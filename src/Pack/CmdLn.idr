module Pack.CmdLn

import Pack.Err
import System.Console.GetOpt

%default total

||| Commands accepted by *pack*. Most of these
||| operate on a list of packages and/or
||| projects with an `.ipkg` file.
public export
data Cmd : Type where
  Build          : Cmd
  CheckDB        : Cmd
  Exec           : Cmd
  Install        : Cmd
  InstallApp     : Cmd
  InstallWithSrc : Cmd
  PrintHelp      : Cmd
  SwitchRepo     : Cmd
  Typecheck      : Cmd
  UpdateDB       : Cmd

export
Show Cmd where
  show Build          = "Build"
  show CheckDB        = "CheckDB"
  show Exec           = "Exec"
  show Install        = "Install"
  show InstallApp     = "InstallApp"
  show InstallWithSrc = "InstallWithSrc"
  show PrintHelp      = "PrintHelp"
  show SwitchRepo     = "SwitchRepo"
  show Typecheck      = "Typecheck"
  show UpdateDB       = "UpdateDB"

||| Program configuration
public export
record Config where
  constructor MkConfig

  ||| Directory where the *pack* DB and installed
  ||| libraries and executables reside
  packDir   : String

  ||| The pack command to run
  cmd       : Cmd

  ||| Command line arguments when running
  ||| an executable
  args      : String

  ||| The packages to build or install
  packages  : List String

  ||| Database to use
  dbVersion : String

export
Show Config where
  show c = """
    MkConfig {
      cmd       := \{show c.cmd}
    , packDir   := \{show c.packDir}
    , dbVersion := \{show c.dbVersion}
    , packages  := \{show c.packages}
    , args      := \{show c.args}
    }
    """

||| Initial configuration.
export
init : (dir : String) -> (db : String) -> (pkgs : List String) -> Config
init dir db pkgs = MkConfig {
    cmd           = PrintHelp
  , packDir       = dir
  , dbVersion     = db
  , packages      = pkgs
  , args          = ""
  }

||| Root directory where files generated by *pack*
||| are stored.
export
rootDir : (conf : Config) => String
rootDir = conf.packDir

||| Temporary directory used for building packages.
export
tmpDir : Config => String
tmpDir = "\{rootDir}/tmp"

||| Directory where databases are stored.
export
dbDir : Config => String
dbDir = "\{rootDir}/db"

--------------------------------------------------------------------------------
--          Applying Command Line Args
--------------------------------------------------------------------------------

setCmd : Cmd -> Config -> Config
setCmd c = {cmd := c}

dir : String -> Config -> Config
dir s = {packDir := s}

setArgs : String -> Config -> Config
setArgs s = {args := s}

setDB : String -> Config -> Config
setDB s = {dbVersion := s}

-- command line options with description
descs : List $ OptDescr (Config -> Config)
descs = [ MkOpt ['h'] ["help"]      (NoArg $ setCmd PrintHelp)
           "Print this help text"

        , MkOpt [] ["pack-dir"]   (ReqArg dir "<dir>")
           """
           Directory where pack stores its database and
           installed packages. This defaults to \"$PACK_DIR\"
           (if set) or \"$HOME/.pack\" otherwise.
           """

        , MkOpt [] ["switch"]   (NoArg $ setCmd SwitchRepo)
           """
           Change the repository `$PACK_DIR/bin` points
           to. If `$PACK_DIR/bin` is on your path, all applications
           installed for the given repository (including Idris2
           itself) will be on your path as well.
           Note: This will install Idris2 and *pack* form the
           given repository so it may take some time.
           """

        , MkOpt [] ["exec"]   (NoArg $ setCmd Exec)
           """
           Build and run an executable given either as
           an `.ipkg` file or a known package from the
           database. Use `--args` to specify command line
           arguments for the executable.
           """

        , MkOpt [] ["check-db"]   (NoArg $ setCmd CheckDB)
           """
           Check the given package collection by freshly
           building and installing its designated Idris2
           followed by installing all listed packages.
           """

        , MkOpt [] ["build"]   (NoArg $ setCmd Build)
           "Build a local package given as an `.ipkg` file"

        , MkOpt [] ["typecheck"]   (NoArg $ setCmd Typecheck)
           "Typecheck a local package given as an `.ipkg` file"

        , MkOpt [] ["install"]   (NoArg $ setCmd Install)
           "Install the given package(s) or local .ipkg files"

        , MkOpt [] ["install-with-src"]   (NoArg $ setCmd InstallWithSrc)
           "Install the given package(s) or local .ipkg files"

        , MkOpt [] ["install-app"]   (NoArg $ setCmd InstallApp)
           "Install the given executable(s)"

        , MkOpt [] ["update-db"]   (NoArg $ setCmd UpdateDB)
           "Update the pack data base"

        , MkOpt [] ["args"]   (ReqArg setArgs "<args>")
           """
           Command line arguments to be passed to an
           executable run with `--exec`. Make sure to quote
           these, when passing several arguments.
           """

        , MkOpt ['p'] ["package-set"]   (ReqArg setDB "<db>")
           """
           Set the curated package set to use. At the
           moment, this defaults to `HEAD`, the latest commits
           of all packages will be used. This is bound to change
           once we have a reasonably stable package set.
           """
        ]

||| Given a root directory for *pack* and a db version,
||| generates the application
||| config from a list of command line arguments.
export
applyArgs : (dir : String) -> (db : String) -> List String -> Either PackErr Config
applyArgs dir db args =
  case getOpt RequireOrder descs args of
       MkResult opts n  []      []       =>
         Right $ foldl (flip apply) (init dir db n) opts
       MkResult _    _ (u :: _) _        => Left (UnknownArg u)
       MkResult _    _ _        (e :: _) => Left (ErroneousArg e)

--------------------------------------------------------------------------------
--          Usage Info
--------------------------------------------------------------------------------

version : String
version = "0.0.1"

progName : String
progName = "pack"

usage : String
usage = "Usage: " ++ progName ++ " [options] [packages]\n\nOptions:\n"

||| Application info printed with the `--help` action.
export
usageInfo : String
usageInfo = usageInfo usage descs
